(require (for-syntax racket/base racket/list racket/syntax))


(provide ;;BIM extensions
 beam
 column
 ;;door
 roof
 slab
 ;;wall
 current-level
 default-level-to-level-height
 upper-level
 (struct-out level))

(struct level
  ([height : Real])
  #:type-name Level)

(define current-level (make-parameter (level 0)))

(define default-level-to-level-height (make-parameter 3))

(define (upper-level [lvl : Level (current-level)]
                     [height : Real (default-level-to-level-height)])
  (level (+ (level-height lvl) height)))

(struct bim-family
  ([path : String]
   [map : (Listof (Cons Keyword (Option Any)))]
   [layer : Layer])
  #:type-name BIM-Family)

(define (create-layer-from-path [path : String]) : Layer
  (create-layer (last (regexp-match #rx"([a-zA-Z]+).rfa" path))))

(define-for-syntax (build-name fmt id)
  (format-id id #:source id fmt (syntax-e id)))

(define-for-syntax (build-keyword id)
  (string->keyword (symbol->string (syntax->datum id))))

(define-syntax (def-bim-family stx)
  (syntax-case stx ()
    [(def name (param ...))
     (with-syntax ([struct-name (build-name "~A-family" #'name)]
                   [instance-name (build-name "~A-family-element" #'name)]
                   [load-name (build-name "load-~A-family" #'name)]
                   [default-name (build-name "default-~A-family" #'name)]
                   [layer-name (string-titlecase (symbol->string (syntax-e #'name)))]
                   [([param-name param-key param-type param-default class-comb instance-comb] ...)
                    (map (lambda (p)
                           (syntax-case p (:)
                             [[name : type default]
                              (with-syntax ([key (build-keyword #'name)])
                                #'[name key type default
                                        (key [name : (Option Any) #f])
                                        (key [name : type default])])]
                             #;[[name : type] #'[name type]]))
                         (syntax->list #'(param ...)))])
       (with-syntax ([class-params
                      (append* (map syntax->list (syntax->list #'(class-comb ...))))]
                     [instance-params
                      (append* (map syntax->list (syntax->list #'(instance-comb ...))))])
         (syntax/loc stx
           (begin
             (provide (struct-out struct-name)
                      default-name
                      load-name
                      instance-name)
             (struct struct-name bim-family
               ([param-name : param-type] ...))
             (define default-name (make-parameter (struct-name "" '() (create-layer layer-name) param-default ...)))
             (define (load-name [path : String] . class-params)
               (struct-name path
                            (list (cons 'param-key param-name) ...)
                            (create-layer-from-path path)
                            param-default ...))
             (define (instance-name [family : BIM-Family] . instance-params)
               (struct-name (bim-family-path family)
                            (bim-family-map family)
                            (bim-family-layer family)
                            param-name ...))))))]))

(def-bim-family beam
  ([width : Real 10]
   [height : Real 10]))

(def-bim-family wall ())
(def-bim-family slab
  ([thickness : Real 1]))
(def-bim-family roof ())

(def-bim-family column
  ([width : Real 10]))

(def-bim-family door ())

(def-shape (beam [p0 : Loc] [p1 : Loc] [family : Beam-Family (default-beam-family)])
  (let ((s (right-cuboid (loc-in-world p0) (beam-family-width family) (beam-family-height family) (loc-in-world p1))))
    (shape-layer s (bim-family-layer family))
    s))

(def-shape (column [center : Loc]
                   [bottom-level : Level (current-level)]
                   [top-level : Level (upper-level bottom-level)]
                   [family : Column-Family (default-column-family)])
  (let ((width (column-family-width family)))
    (let ((s (box (+xyz (loc-in-world center) (/ width -2) (/ width -2) (level-height bottom-level))
                  width
                  width
                  (- (level-height top-level) (level-height bottom-level)))))
      (shape-layer s (bim-family-layer family))
      s)))

;(def-shape (door))
(def-shape (slab [vertices : Locs] [level : Level (current-level)] [family : Slab-Family (default-slab-family)])
  (let ((s (extrusion
            (surface-polygon (map (lambda ([p : Loc])
                                    (+z (loc-in-world p) (level-height level)))
                                  vertices))
            (slab-family-thickness family))))
    (shape-layer s (bim-family-layer family))
    s))
        
(def-shape (roof [vertices : Locs] [level : Level (current-level)] [family : Roof-Family (default-roof-family)])
  (let ((s (extrusion
            (surface-polygon (map (lambda ([p : Loc])
                                    (+z (loc-in-world p) (level-height level)))
                                  vertices)))))
    (shape-layer s (bim-family-layer family))
    s))